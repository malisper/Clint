;;;; This file defines a macro (define-cxrs), which is capable of
;;;; defining all of the cxr functions (car, cdr, cadr, etc, and (setf
;;;; car), (setf cdr), (setf cadr), etc) at one time.

(in-package :clint)

(defun generate-n (xs n)
  "Generates all possible permutations of N elements of XS."
  (if (eql n 0)
      '(())
      (mapcan (lambda (elt)
                (mapcar (lambda (perm)
                          (cons elt perm))
                        (generate-n xs (- n 1))))
              xs)))

(defun range (n)
  "Generate the range from 1 to N."
  (loop for i from 1 to n collect i))

(defun generate-cxrs (n)
  "Generates all of the symbols for cxrs up to N levels deep,
   excluding cr."
  (mapcan (lambda (i)
            (mapcar (lambda (ads)
                      (intern (format nil "C窿尧徜螬┅ㄧ孱弪狒瀛Ж洎椹┅蜥铉瞟┅ㄤ彐磲泸溴骈铄泺蝮瞟⒛彐轭犰镦翳泺蝮铄篝邃骝镯麸萎啜痱镧括磲疸狎灬礅溽ㄣ颟啜痱镧ㄡ滗痱轫К泺ㄦ矧磲铋⒁弭躜翳镦禺泺颟ㄤ彐痱轫轸轹瀛骖箦翩泺颟鲠ㄦ矧磲铋⒂弭翳鲠祯镦翳镦麸至坍泺颟箦翩ì泺鲠飑┅ㄧ孱弪狒瀛泺蝮瞟┅